# Default command lists all available recipes
_default:
    @just --list

alias b := bump
alias c := clean
alias d := dist
alias h := hooks
alias q := check
alias t := test

# clean all python build/compilation files and directories
clean:
    find . -name '*.pyc' -exec rm --force {} +
    find . -name '*.pyo' -exec rm --force {} +
    find . -name '*~' -exec rm --force {} +
    rm --force .coverage
    rm --force --recursive .pytest_cache
    rm --force --recursive build/
    rm --force --recursive dist/
    rm --force --recursive *.egg-info

# install dependencies in local venv
venv:
    uv sync --group test --group dev

# run test with coverage
test:
    uv run pytest tests/ -rsx --verbose --color=yes

# lint python code using ruff
[private]
lint:
    uvx ruff check . --fix

# format python code using ruff
[private]
format:
    uvx ruff format .

# run the type checker ty
[private]
types:
    uvx ty check

# lint and format python code (ruff) and run the type checker (ty)
check: lint format types

# setup the pre-commit hooks
hooks:
    uvx pre-commit install

[confirm("Do you really want to bump? (y/n)")]
[private]
prompt-confirm:

# bump the version, commit the changes and add a tag (increment can be major, minor, patch,...)
bump INCREMENT:
    @uv version --bump {{ INCREMENT }} --dry-run
    @just prompt-confirm
    uv version --bump {{ INCREMENT }}
    git add pyproject.toml
    git commit -m "Bumped version to `uv version --short`"
    git tag "v`uv version --short`"
    @echo "{{ GREEN }}{{ BOLD }}Version has been bumped to `uv version --short`.{{ NORMAL }}"

# build the source distribution and wheel file with uv (bump version first)
dist:
    uv build

# initialize a git repo and add all files
init:
    git init
    git add .
    git commit -m "initial commit"
    @echo "{{ GREEN }}{{ BOLD }}Git has been initialized{{ NORMAL }}"
